#!/usr/bin/perl

use strict;
use warnings;

use File::Basename;
use XML::Writer;
use IO::Handle;
use autodie qw(open close);

use lib dirname(__FILE__) . '/../lib';

use Twarchiver::Schema;

my $dbname = shift;
die "No db" unless (-f $dbname);

my $dumpfile = shift || dirname(__FILE__) . '/db_dump.xml';

my $schema = Twarchiver::Schema->connect("dbi:SQLite:dbname=$dbname");

open( my $output, '>', $dumpfile);
my $writer = XML::Writer->new(
    OUTPUT => $output, 
    DATA_MODE => 1,
    DATA_INDENT => 2,
    ENCODING => 'utf-8',
);
$writer->startTag("userdata");

# First dump the users

my $users = $schema->resultset('User')->search();
$writer->startTag("users");

while (my $user = $users->next()) {
    my %attributes = (
        username => $user->username,
        passhash => $user->passhash,
        created_at => $user->created_at->epoch,
        last_login => $user->last_login->epoch,
        last_update => $user->last_update->epoch,
    );
    $writer->startTag('user', %attributes);
    my $twitter = $user->twitter_account;
    %attributes = (
        screen_name => $twitter->screen_name,
        tweet_total => $twitter->tweet_total,
        created_at => $twitter->created_at->epoch,
        profile_image_url => $twitter->profile_image_url,
        profile_bkg_url => $twitter->profile_bkg_url,
        access_token => $twitter->access_token,
        access_token_secret => $twitter->access_token_secret,
    );
    $writer->startTag('twitter_account', %attributes);
    my $tweets = $twitter->tweets;
    while (my $tweet = $tweets->next()) {
        my %attributes = (
            id => $tweet->tweet_id,
            retweeted_count => $tweet->retweeted_count,
            text => $tweet->text,
            tweeted_at => $tweet->tweeted_at->epoch,
        );
        $writer->startTag('tweet', %attributes);
        my $tags = $tweet->tags;
        while (my $tag = $tags->next()) {
            $writer->emptyTag('tag', tag_text => $tag->tag_text);
        }
        my $mentions = $tweet->mentions;
        while (my $mention = $mentions->next()) {
            $writer->emptyTag('mention', screen_name => $mention->screen_name);
        }
        my $hashtags = $tweet->hashtags;
        while (my $hashtag = $hashtags->next()) {
            $writer->emptyTag('hashtag', topic => $hashtag->topic);
        }
        my $urls = $tweet->urls;
        while (my $url = $urls->next()) {
            $writer->emptyTag('url', address => $url->address);
        }
        $writer->endTag();
    }
    $writer->endTag();
    $writer->endTag();
}

$writer->endTag();
$writer->endTag();
# Then dump the tweets, with their mentions, urls, tags and hashtags
# inlined.







